<?xml version="1.0" encoding="UTF-8"?>

<!--
/* ***** BEGIN LICENCE BLOCK *****
 * Version: CePL 1.1
 * 
 * The contents of this file are subject to the Celtx Public License
 * Version 1.1 (the "License"); you may not use this file except in
 * compliance with the License. You may obtain a copy of the License at
 * http://www.celtx.com/CePL/
 * 
 * Software distributed under the License is distributed on an "AS IS"
 * basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See
 * the License for the specific language governing rights and limitations
 * under the License.
 * 
 * The Original Code is Celtx Script Manager.
 * 
 * The Initial Developer of the Original Code is Chad House and 4067479
 * Canada Inc. t/a CELTX.
 * 
 * Portions created by Chad House are Copyright (C) 2000-2004 Chad House,
 * parts created by Celtx are Copyright (C) 4067479 Canada Inc. All Rights
 * Reserved.
 * 
 * Contributor(s):
 *
 ***** END LICENCE BLOCK ***** */
-->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                version="1.0">

  <xsl:output method="xml" version="1.0" encoding="UTF-8"/>

  <xsl:variable name="lcase">abcdefghijklmnopqrstuvwxyz</xsl:variable>
  <xsl:variable name="ucase">ABCDEFGHIJKLMNOPQRSTUVWXYZ</xsl:variable>

<!--
  <xsl:template match="html">
    <xsl:variable name="title" select="normalize-space(head/title)"/>
    <script title="{$title}">
      <xsl:apply-templates select="body/p[@class='sceneheading']"/>
    </script>
  </xsl:template>
-->

  <xsl:template match="p[@class='sceneheading']">
    <xsl:variable name="prev" select="preceding-sibling::p[position()=1]"/>
    <scene id="{@id}" title="{translate(normalize-space(.), $lcase, $ucase)}"
           ordinal="{count(preceding-sibling::p[@class='sceneheading'])+1}">
      <xsl:if test="$prev/@class = 'act'">
        <xsl:variable name="actname"
                  select="translate(normalize-space($prev), $lcase, $ucase)"/>
        <xsl:attribute name="act"><xsl:value-of select="$actname"/></xsl:attribute>
      </xsl:if>
      <xsl:apply-templates select="span[@class='note']"/>
      <xsl:variable name="next" select="following-sibling::p[position()=1]"/>
      <xsl:if test="$next/@class != 'sceneheading'">
        <xsl:apply-templates select="$next"/>
      </xsl:if>
    </scene>
  </xsl:template>

  <xsl:template match="p[@class='act']">
    <xsl:variable name="next" select="following-sibling::p[position()=1]"/>
    <xsl:if test="$next/@class != 'sceneheading'">
      <xsl:apply-templates select="$next"/>
    </xsl:if>
  </xsl:template>

  <xsl:template match="p[@class='character']">
    <speech speaker="{translate(normalize-space(.), $lcase, $ucase)}">
      <xsl:apply-templates select="span[@class='note']"/>
      <xsl:variable name="next" select="following-sibling::p[position()=1]"/>
      <xsl:if test="$next/@class = 'dialog' or $next/@class = 'parenthetical'">
        <xsl:apply-templates select="$next" mode="speech"/>
      </xsl:if>
    </speech>
    <xsl:variable name="next" select="following-sibling::p[@class != 'dialog'
                        and @class != 'parenthetical'][position()=1]"/>
    <xsl:if test="$next/@class != 'sceneheading'">
      <xsl:apply-templates select="$next"/>
    </xsl:if>
  </xsl:template>

  <xsl:template match="p[@class='dialog' or @class='parenthetical']"
                mode="speech">
    <xsl:element name="{@class}"><xsl:apply-templates/></xsl:element>
    <xsl:variable name="next" select="following-sibling::p[position()=1]"/>
    <xsl:if test="$next/@class = 'dialog' or $next/@class = 'parenthetical'">
      <xsl:apply-templates select="$next" mode="speech"/>
    </xsl:if>
  </xsl:template>

  <xsl:template match="p">
    <xsl:choose>
      <xsl:when test="@class and @class!=''">
        <xsl:element name="{@class}"><xsl:apply-templates/></xsl:element>
      </xsl:when>
      <xsl:otherwise>
        <xsl:element name="text"><xsl:apply-templates/></xsl:element>
      </xsl:otherwise>
    </xsl:choose>
    <xsl:variable name="next" select="following-sibling::p[position()=1]"/>
    <xsl:if test="$next/@class != 'sceneheading'">
      <xsl:apply-templates select="$next"/>
    </xsl:if>
  </xsl:template>

  <xsl:template match="span[@class='note']">
    <note>
      <xsl:value-of select="@text"/>
    </note>
  </xsl:template>

  <xsl:template match="span[@style]">
    <span>
      <xsl:for-each select="@*">
        <xsl:copy/>
      </xsl:for-each>
      <xsl:apply-templates select="node()"/>
    </span>
  </xsl:template>

</xsl:stylesheet>
